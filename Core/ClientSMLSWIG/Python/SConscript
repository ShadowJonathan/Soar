#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#
import sysconfig
import os
import os.path
import sys

Import('env')
clone = env.Clone()
clone['SWIGPATH'] = clone['CPPPATH']
clone['SWIGFLAGS'] = ['-c++', '-python']
if GetOption('nosvs'):
    clone['SWIGFLAGS'].append('-DNO_SVS')
python_sml_alias = clone['SML_PYTHON_ALIAS']

inc_path = sysconfig.get_path('include')
if os.name == 'nt':
    lib_path = os.path.join(sysconfig.get_config_vars('BINDIR')[0], 'libs')
    pylib = 'python' + sysconfig.get_config_vars('VERSION')[0]
else:
    lib_path = sysconfig.get_config_vars('LIBDIR')[0]
    pylib = sysconfig.get_config_vars('LIBRARY')[0]
lib_install_dir = clone['OUT_DIR']

clone.Append(CPPPATH = inc_path)

if not env['ENSCONS_ACTIVE']:
    clone.Append(LIBPATH = lib_path, LIBS = pylib)

if os.name == 'posix':
    clone.Append(CPPFLAGS = Split('-Wno-unused -fno-strict-aliasing'))

if sys.platform == 'darwin':
    clone.Replace(SHLINKFLAGS = Split('$LINKFLAGS -bundle -flat_namespace -undefined suppress'))
    clone.Replace(SHLIBSUFFIX = ['.so'])

name = 'Python_sml_ClientInterface'
interface = env.File(name + '.i')
source = env.File(name + '.py')

if os.name == 'nt':
    # the [:1] at the end throws away the .exp and .lib files that are generated
    shlib = clone.SharedLibrary(name, interface, SHLIBPREFIX = '_', SHLIBSUFFIX = '.pyd')[:1]
else:
    shlib = clone.SharedLibrary(name, interface, SHLIBPREFIX = '_')
install_source = env.Install(lib_install_dir, source)
install_lib = env.Install(lib_install_dir, shlib)
install_test = env.Install(lib_install_dir, env.File('TestPythonSML.py'))

env.Alias(python_sml_alias, install_lib + install_source + install_test)

python_shlib = shlib
python_source = source
Export('python_shlib')
Export('python_source')
